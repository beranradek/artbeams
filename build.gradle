buildscript {
    ext {
        springBootVersion = '2.4.5'
        kotlinVersion = '1.5.31'
    }
    repositories {
        jcenter()
    }
    dependencies {
        // The Spring Boot Gradle Plugin provides Spring Boot support in Gradle, 
        // allowing you to package executable jar or war archives, 
        // run Spring Boot applications and use the dependency management 
        // provided by spring-boot-dependencies.
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'org.xbery'
version = '1.0.6-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11
def jvmTargetVersion = JavaVersion.VERSION_11
targetCompatibility = jvmTargetVersion
kotlinVersion = '1.5.31'

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    // Kotlin language
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion")
    implementation("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion") // mandatory as of Spring Framework 5

    // Web libraries
    implementation("org.webjars:webjars-locator:0.36")
    implementation("org.webjars:bootstrap:4.1.3")

    // Spring Boot: Thanks to Spring Boot Gradle Plugin, versions can be omitted
    // Support for Web MVC, Freemarker templates, database layer, security
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-freemarker")
    implementation("org.springframework.boot:spring-boot-starter-jdbc")
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-cache")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    developmentOnly("org.springframework.boot:spring-boot-devtools")

    // Database and repository libraries
    runtimeOnly("org.postgresql:postgresql:9.4.1208")
    implementation("org.xbery:overview-repo-sql:1.2.0")

    // Forms
    implementation("net.formio:formio:1.6.5") {
        exclude group: 'javax.servlet', module: 'servlet-api' // used version from Spring Boot
    }

    // Working with text/HTML
    implementation("org.apache.commons:commons-text:1.6")
    implementation("commons-io:commons-io:2.9.0")

    // Evernote integration
    implementation("com.evernote:evernote-api:1.25.1")

    // Markdown support
    implementation("com.vladsch.flexmark:flexmark:0.50.28")
    implementation("com.vladsch.flexmark:flexmark-ext-attributes:0.50.28")
    implementation("javax.inject:javax.inject:1")

    // Detection of client - browser capabilities
    implementation("com.blueconic:browscap-java:1.2.11")

    // HTTP client for communication with external services
    implementation("org.apache.httpcomponents:httpclient:4.5.10")

    // Working with PDF documents
    implementation("org.apache.pdfbox:pdfbox:2.0.17")

    // Working with webp and other images
    implementation("com.sksamuel.scrimage:scrimage-core:4.1.1")
    implementation("com.sksamuel.scrimage:scrimage-webp:4.1.1")
    implementation("com.drewnoakes:metadata-extractor:2.16.0")

    // Testing Spring Boot application
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.junit.jupiter:junit-jupiter:5.8.0")
}

// Task stage for Heroku deployment
task stage(dependsOn: ['build', 'clean'])

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        // By default, types from Java APIs used in Kotlin are recognized as platform types for which null-checks are relaxed.
        // Kotlin support for JSR 305 annotations + Spring nullability annotations provide null-safety for the whole Spring Framework API
        // to Kotlin developers, with the advantage of dealing with null related issues at compile time.
        freeCompilerArgs = ["-Xjsr305=strict", "-Xopt-in=kotlin.RequiresOptIn"]
        jvmTarget = jvmTargetVersion
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

build.mustRunAfter clean
